
// Root URL for model archives
def models = ['object_detection/ssd_mobilenet_v1_android_export.zip']
// hard coded model files
def MODEL_URL = 'https://storage.googleapis.com/download.tensorflow.org/models'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'de.undercouch:gradle-download-task:3.2.0'
    }
}

import de.undercouch.gradle.tasks.download.Download

task downloadFile(type: Download){
    for (f in models) {
        src "${MODEL_URL}/" + f
    }
    dest new File(project.ext.TMP_DIR)
    overwrite true
}

task extractModels(type: Copy) {
    if (!new File(project.ext.TMP_DIR).exists()) {
        new File(project.ext.TMP_DIR).mkdirs()
    }

    if (!new File(project.ext.ASSET_DIR).exists()) {
        new File(project.ext.ASSET_DIR).mkdirs()
    }

    for (f in models) {
        def localFile = f.split("/")[-1]
        from zipTree(project.ext.TMP_DIR + '/' + localFile)
    }

    into file(project.ext.ASSET_DIR)
    fileMode  0644
    exclude '**/LICENSE'

    def needDownload = false
    for (f in models) {
        def localFile = f.split("/")[-1]
        if (!(new File(project.ext.TMP_DIR + '/' + localFile)).exists()) {
            needDownload = true
        }
    }

    if (needDownload) {
        dependsOn downloadFile
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleDebug') {
        task.dependsOn 'extractModels'
    }
    if (task.name == 'assembleRelease') {
        task.dependsOn 'extractModels'
    }
}

